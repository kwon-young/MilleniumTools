# detect the OS
if(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
    set(MiT_OS_WINDOWS 1)

    # detect the architecture (note: this test won't work for cross-compilation)
    include(CheckTypeSize)
    check_type_size(void* SIZEOF_VOID_PTR)
    if("${SIZEOF_VOID_PTR}" STREQUAL "4")
        set(ARCH_32BITS 1)
    elseif("${SIZEOF_VOID_PTR}" STREQUAL "8")
        set(ARCH_64BITS 1)
    else()
        message(FATAL_ERROR "Unsupported architecture")
        return()
    endif()
elseif(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    set(MiT_OS_UNIX 1)
else()
    message(FATAL_ERROR "Unsupported operating system")
    return()
endif()

# detect the compiler and its version
# Note: on some platforms (OS X), CMAKE_COMPILER_IS_GNUCXX is true
# even when CLANG is used, therefore the Clang test is done first
if(CMAKE_CXX_COMPILER MATCHES ".*clang[+][+]" OR CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
   # CMAKE_CXX_COMPILER_ID is an internal CMake variable subject to change,
   # but there is no other way to detect CLang at the moment
   set(MiT_COMPILER_CLANG 1)
   execute_process(COMMAND "${CMAKE_CXX_COMPILER}" "--version" OUTPUT_VARIABLE CLANG_VERSION_OUTPUT)
   string(REGEX REPLACE ".*clang version ([0-9]+\\.[0-9]+).*" "\\1" MiT_CLANG_VERSION "${CLANG_VERSION_OUTPUT}")
elseif(CMAKE_COMPILER_IS_GNUCXX)
    set(MiT_COMPILER_GCC 1)
    execute_process(COMMAND "${CMAKE_CXX_COMPILER}" "-dumpversion" OUTPUT_VARIABLE GCC_VERSION_OUTPUT)
    string(REGEX REPLACE "([0-9]+\\.[0-9]+).*" "\\1" MiT_GCC_VERSION "${GCC_VERSION_OUTPUT}")
    execute_process(COMMAND "${CMAKE_CXX_COMPILER}" "--version" OUTPUT_VARIABLE GCC_COMPILER_VERSION)
    string(REGEX MATCHALL ".*(tdm[64]*-[1-9]).*" MiT_COMPILER_GCC_TDM "${GCC_COMPILER_VERSION}")
    execute_process(COMMAND "${CMAKE_CXX_COMPILER}" "-dumpmachine" OUTPUT_VARIABLE GCC_MACHINE)
    string(STRIP "${GCC_MACHINE}" GCC_MACHINE)
    if(${GCC_MACHINE} MATCHES ".*w64.*")
        set(MiT_COMPILER_GCC_W64 1)
    endif()
elseif(MSVC)
    set(MiT_COMPILER_MSVC 1)
    if(MSVC_VERSION EQUAL 1400)
        set(MiT_MSVC_VERSION 8)
    elseif(MSVC_VERSION EQUAL 1500)
        set(MiT_MSVC_VERSION 9)
    elseif(MSVC_VERSION EQUAL 1600)
        set(MiT_MSVC_VERSION 10)
    elseif(MSVC_VERSION EQUAL 1700)
        set(MiT_MSVC_VERSION 11)
    elseif(MSVC_VERSION EQUAL 1800)
        set(MiT_MSVC_VERSION 12)
    elseif(MSVC_VERSION EQUAL 1900)
        set(MiT_MSVC_VERSION 14)
    endif()
else()
    message(FATAL_ERROR "Unsupported compiler")
    return()
endif()

# define the install directory for miscellaneous files
if(MiT_OS_WINDOWS OR MiT_OS_IOS)
    set(INSTALL_MISC_DIR .)
elseif(MiT_OS_LINUX OR MiT_OS_FREEBSD OR MiT_OS_MACOSX)
    set(INSTALL_MISC_DIR share/MiT)
elseif(MiT_OS_ANDROID)
    set(INSTALL_MISC_DIR ${ANDROID_NDK}/sources/mit)
endif()
